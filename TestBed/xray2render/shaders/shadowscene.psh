ps.2.0

// ambient color
def c17, 0.25, 0.25, 0.25, 1.0
// (0, 1, sample weight (1/16), z-bias)
def c18, 0.0, 1.0, 0.0625, +0.001

dcl t0.xyzw
dcl t1.x
dcl v0.rgba

dcl_2d s0

// add z-bias 
add r8, t1.x, c18.w

// coordinates for first two rows
add r0, t0, c0.xyzw
add r1, t0, c0.wzyx
add r2, t0, c1.xyzw
add r3, t0, c1.wzyx
add r4, t0, c2.xyzw
add r5, t0, c2.wzyx
add r6, t0, c3.xyzw
add r7, t0, c3.wzyx

// fetch two rows
texld r0, r0, s0
texld r1, r1, s0
texld r2, r2, s0
texld r3, r3, s0
texld r4, r4, s0
texld r5, r5, s0
texld r6, r6, s0
texld r7, r7, s0

// compare first row
sub r0.r, r8, r0.r
sub r0.g, r8, r1.r
sub r0.b, r8, r2.r
sub r0.a, r8, r3.r
cmp r9, -r0, c18.y, c18.x

// sum of weighted first row
dp4 r10, r9, c18.z

// compare second row
sub r0.r, r8, r4.r
sub r0.g, r8, r5.r
sub r0.b, r8, r6.r
sub r0.a, r8, r7.r
cmp r9, -r0, c18.y, c18.x

// sum of weighted second row
dp4 r9, r9, c18.z

// accumulate second row
add r10, r10, r9

// coordinates for third and fourth rows
add r0, t0, c4.xyzw
add r1, t0, c4.wzyx
add r2, t0, c5.xyzw
add r3, t0, c5.wzyx
add r4, t0, c6.xyzw
add r5, t0, c6.wzyx
add r6, t0, c7.xyzw
add r7, t0, c7.wzyx

// fetch two rows
texld r0, r0, s0
texld r1, r1, s0
texld r2, r2, s0
texld r3, r3, s0
texld r4, r4, s0
texld r5, r5, s0
texld r6, r6, s0
texld r7, r7, s0

// compare third row
sub r0.r, r8, r0.r
sub r0.g, r8, r1.r
sub r0.b, r8, r2.r
sub r0.a, r8, r3.r
cmp r9, -r0, c18.y, c18.x

// sum of weighted third row
dp4 r9, r9, c18.z

// accumulate third row
add r10, r10, r9

// compare fourth row
sub r0.r, r8, r4.r
sub r0.g, r8, r5.r
sub r0.b, r8, r6.r
sub r0.a, r8, r7.r
cmp r9, -r0, c18.y, c18.x

// sum of weighted fourth row
dp4 r9, r9, c18.z

// accumulate fourth row
add r0, r10, r9

// add lighting
mad r0, r10, v0, c17

// output
mov oC0, r0
